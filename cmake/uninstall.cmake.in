# Implements "uninstall" make target
#
# See https://gitlab.kitware.com/cmake/community/-/wikis/FAQ#can-i-do-make-uninstall-with-cmake
#
if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR
    "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt"
  )
endif()

# Load install manifest
file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")

# Append files installed by setup.py to list of files to uninstall
#
# Ideally this should be obtained from the install manifest of setup.py,
# but setup.py seems not to produce such a manifest, or at least I cannot
# find it. For now we just manually append additional files to uninstall.
list(APPEND files "@CMAKE_INSTALL_PREFIX@/bin/dlite-validate")

# Uninstall
foreach(file ${files})
  message(STATUS "Uninstalling $ENV{DESTDIR}${file}")
  if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
    execute_process(
      COMMAND "@CMAKE_COMMAND@" ARGS "-E remove \"$ENV{DESTDIR}${file}\""
      RESULT_VARIABLE rm_retval
      )
    if(NOT "${rm_retval}" STREQUAL 0)
      message(FATAL_ERROR "Problem when removing $ENV{DESTDIR}${file}")
    endif()
  else()
    message(STATUS "File $ENV{DESTDIR}${file} does not exist.")
  endif()
endforeach()


# Also uninstall empty directories
foreach(file ${files})
  get_filename_component(dir ${file} DIRECTORY)
  if(EXISTS ${dir})
    file(GLOB_RECURSE content LIST_DIRECTORIES false "${dir}/*")
    if("${content}" STREQUAL "")
      file(REMOVE_RECURSE ${dir})
      message(STATUS "Removing empty directory ${dir}")
    endif()
  endif()
endforeach()
